DAYS:=20;
W:=0.75;
M:=10; {短期}
K:=20; {上下界}
WEEKLY:=PERIOD=5 OR PERIOD=6;
MONTHLY:=PERIOD=6 OR PERIOD=7;

{脚本语言:通达信}
LLMIN:=MIN(O, C);
HH:=MAX(O, C);
LL:=IF(LLMIN=0, IF(HH=0,-0.1,HH), LLMIN); {价格=0 对数坐标可能出问题}
MID:=(L + H)/2;{K线中值}

{通道线}
MA20:=MA(CLOSE, DAYS);
DIS:=STDP(CLOSE,DAYS);
UPPER:= MA20 +2.2*DIS;
LOWER:= MA20-2.2*DIS;
NORMAL:=(LOWER - C)/C > -0.5;{兼容GME、AMC通道线过大}
IF(WEEKLY, UPPER, DRAWNULL),COLORBLACK,POINTDOT;
IF(WEEKLY AND NORMAL AND LOWER>0, LOWER, DRAWNULL), COLORBLACK, POINTDOT;
PLOYLINE(PERIOD=6,(HHV(H, 52) +LLV(L, 52))/2),DOTLINE,LINETHICK2;{自然支撑压力:52周范围中线}

MAS:MA20,DOTLINE,COLORBLUE;
MAM:MA(CLOSE, DAYS*3),DOTLINE,COLORYELLOW;
MAL:=MA(CLOSE, DAYS*6);PLOYLINE(PERIOD=7,MAL),DOTLINE, COLORMAGENTA;
EMAS:EMA(CLOSE, DAYS),COLORBLUE;
EMAM:EMA(CLOSE, DAYS*3),COLORYELLOW;
PLOYLINE(1,EMA(CLOSE, DAYS*6)),COLORMAGENTA;

{趋势红绿柱}
STICKLINE(1, LL, HH, W, 0), COLORBCBCBC;{交易区域}
BEAR:=C<EMAS AND MAS < REF(MAS, 1);  {破线、拐头}
BULL:=C>EMAS AND MAS > REF(MAS, 1);  {破线、拐头}
BEARX:=(BEAR AND EMAS < EMAM); {多头交叉}
BULLX:= (BULL  AND EMAS > EMAM); {空头交叉}
STICKLINE(BULLX, LL, HH, W, 0), COLORGREEN;
STICKLINE(BEARX, LL, HH, W, 0),COLORF47983;{桃红色}

{新低策略: 中期最低值 + 当日跌幅超2% + 破通道线}
{LLL:= C=LLV(C,DAYS*3) AND C/REF(C,1)<0.98 AND C<LOWER;}
{L0:STICKLINE(LLL, LL, HH, W, 0),COLORPURPLE;}

CC:=BARSCOUNT(C);
CNT:=TOTALBARSCOUNT;
{通道线测试，主要品种在 90% 以上}
{SUM(IF(LOWER<=C AND C<=UPPER,1,0),CNT)/CNT,NODRAW;}

{抵扣价计算}
STICKLINE(CC=CNT-DAYS-1, L, H, W, 0), COLORBLACK;
STICKLINE(CC=CNT-DAYS*3-1, L, H, W, 0), COLORBLACK;
STICKLINE(CC=CNT-DAYS*6-1, L, H, W, 0), COLORBLACK;

{袋鼠尾:高波动+高成交量+近期最高点+窄幅收盘}
EXT:=(H-L)/L/(REF(H,1)-REF(L,1))*REF(L,1)>2; {价格范围是前一天2倍}
A:=IF(CONST(AMOUNT), AMOUNT, VOLA); {以金额为主，成交量为辅}
MAA:=REF(MA(A, 5), 1);
HVOL:=A/MAA>1.17; {高成交量}
REC:=HHV(H,260)=H OR LLV(L,120)=L; {极值&跌速快于涨速}
NAR:=(HH-LL)/(H-L)<0.4; {窄幅收盘}
OVR:=(C<LOWER AND C > MID) OR (C>UPPER); {破通道线}
T:=EXT AND HVOL AND REC AND NAR AND OVR;
STICKLINE(T, LL, HH, W, 0),COLORYELLOW;{金色袋鼠尾，最上层显示}

{顶部2B}
CNT1:=250;
H1:=HHV(H, CNT1);
HDAY:=HHVBARS(H, CNT1);
UH:=(H-HH) / (LLMIN-L + 0.01);
B1:=H1/REF(H1,1)>1 AND C<O AND REF(HDAY,1)>0 AND HVOL AND UH>1.5; 
STICKLINE(B1, LL, HH, W, 0), COLORPURPLE;
{底部2B}
L1:=LLV(L,CNT1);
LDAY:=LLVBARS(L,CNT1);
LH:=(LLMIN-L - 0.01)/(H-HH);
B2:=0<L1 AND L1/REF(L1,1)<1 AND C>O AND REF(LDAY,1)>0 AND HVOL AND LH>1.5; 
STICKLINE(B2, LL, HH, W, 0),COLOR0000FF;

{四天准则}
UP4:=PERIOD=5  AND UPNDAY(C,4) AND LDAY<4 AND REF(LH>1.1,LDAY) AND REF(C>O,LDAY);
DRAWTEXT(UP4 AND NOT(REF(UP4,1)), H*1.015, '⬆'), COLOR0000FF;
DN4:=PERIOD=5  AND DOWNNDAY(C,4) AND HDAY<4 AND REF(UH>1.1,HDAY) AND REF(C<O,HDAY);
DRAWTEXT(DN4 AND NOT(REF(DN4,1)), L*0.985, '⬇'), COLORPURPLE;

{RSI 相对强弱}
LC:=REF(CLOSE,1);
TEMP1:=MAX(CLOSE-LC,0);
TEMP2:=ABS(CLOSE-LC);
RSI:=SMA(TEMP1,M,1)/SMA(TEMP2,M,1)*100;
R1:=RSI < K; {低位}
R2:=RSI > (100-K); {高位}
WARN:=  HVOL AND (R1 OR R2) AND (H < LOWER OR L > UPPER); {极端信号}
IF(WARN, IF(R1, LOWER, UPPER), DRAWNULL),COLORBLACK,CIRCLEDOT,LINETHICK5;{极端信号}